use ard/decode
use ard/list as List
use ard/maybe
use ard/result as Result

// Only support primitives as values for query variables
type Value = Str | Bool | Int | Float | Void

private extern fn connect(filepath: Str) Dynamic!Str = "SqliteCreateConnection"
private extern fn close_connection(conn: Dynamic) Void!Str = "SqliteClose"
private extern fn execute(conn: Dynamic, sql: Str, values: [Value]) Void!Str = "SqliteExecute"
private extern fn run_query(conn: Dynamic, sql: Str, values: [Value]) [Dynamic]!Str = "SqliteQuery"

extern fn extract_params(sql: Str) [Str] = "SqliteExtractParams"

struct Query {
  conn: Dynamic,
  string: Str,
  params: [Str]
}

impl Query {
  fn prepare(mut query_str: Str, mut values: [Value], args: [Str:Value]) Void!Str {
    for param in @params {
      query_str = query_str.replace_all("@"+param, "?")
      let value = try args.get(param) -> _ { Result::err("Missing parameter: " + param) }
      values.push(value)
    }

    Result::ok(())
  }

  fn run(args: [Str:Value]) Void!Str {
    mut sanitized = @string
    mut values: [Value] = []
    try @prepare(sanitized, values, args)
    execute(@conn, sanitized, values)
  }

  fn all(args: [Str:Value]) [Dynamic]!Str {
    mut sanitized = @string
    mut values: [Value] = []
    try @prepare(sanitized, values, args)
    run_query(@conn, sanitized, values)
  }

  fn first(args: [Str:Value]) Dynamic?!Str {
    let rows = try @all(args)
    match rows.size() {
      0 => Result::ok(maybe::none()),
      _ => Result::ok(maybe::some(rows.at(0)))
    }
  }
}

struct Database {
  conn: Dynamic
  filepath: Str
}

fn open(filepath: Str) Database!Str {
  let conn = try connect(filepath)
  Result::ok(Database{ conn: conn, filepath: filepath })
}

impl Database {
  fn close() Void!Str {
    close_connection(@conn)
  }

  // simple one-off executions where the results aren't needed
  // [note]: could be removed entirely for query.run() once optional params are sorted
  fn exec(sql: Str) Void!Str {
    execute(@conn, sql, List::new<Value>())
  }

  fn query(sql: Str) Query {
    Query{
      conn: @conn,
      string: sql,
      params: extract_params(sql),
    }
  }
}
