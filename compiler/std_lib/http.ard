use ard/maybe
use ard/string as Str

enum Method {
  Get,
	Post,
	Put,
	Del,
	Patch,
	Options,
}

impl Str::ToString for Method {
  fn to_str() Str {
    match @ {
      Method::Get => "GET",
      Method::Post => "POST",
      Method::Put => "PUT",
      Method::Del => "DELETE",
      Method::Patch => "PATCH",
      Method::Options => "OPTIONS",
    }
  }
}

struct Request {
  method:  Method,
	url:     Str,
	headers: [Str:Str],
	body:    Dynamic?,

	// inbound requests have the *http.Request
  raw: Dynamic?,
}

private extern fn get_req_path(req: Dynamic) Str = "GetReqPath"
private extern fn get_path_value(req: Dynamic, name: Str) Str = "GetPathValue"
private extern fn get_query_param(req: Dynamic, name: Str) Str = "GetQueryParam"

impl Request {
  fn path() Str? {
    let raw = try @raw
    maybe::some(get_req_path(raw))
  }

  fn path_param(name: Str) Str {
    let raw = try @raw -> _ { "" }
    get_path_value(raw, name)
  }

  fn query_param(name: Str) Str {
    let raw = try @raw -> _ { "" }
    get_query_param(raw, name)
  }
}

struct Response {
  status:  Int,
	headers: [Str: Str],
	body:    Str,
}

fn Response::new(status: Int, body: Str) Response {
  Response{status: status, body: body, headers: [:] }
}

impl Response {
  fn is_ok() Bool {
    @status >= 200 and @status <= 300
  }
}

private extern fn _send(method: Str, url: Str, body: Dynamic?, headers: [Str:Str]) Response!Str = "HTTP_Send"

fn send(req: Request) Response!Str {
  let method = req.method.to_str()
  _send(method, req.url, req.body, req.headers)
}

type HandlerFn = fn(Request, mut Response)

extern fn serve(port: Int, handlers: [Str:HandlerFn]) Void!Str = "HTTP_Serve"
