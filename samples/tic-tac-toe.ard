use ard/io
use ard/option

struct Board {
  cells: [Str]
}

impl (mut b: Board) {
  fn play(player: Str, pos: Int) {
    b.cells.set(pos, player)
  }
}

impl (b: Board) {
  fn is_full() Bool {
    mut full = true
    for cell in (b.cells) {
      if (cell.is_empty()) {
        full = false
        break
      }
    }
    full
  }

  fn draw() {
    io.print("{{b.cells.at(0)}} | {{b.cells.at(1)}} | {{b.cells.at(2)}}")
    io.print("-------")
    io.print("{{b.cells.at(3)}} | {{b.cells.at(4)}} | {{b.cells.at(5)}}")
    io.print("-------")
    io.print("{{b.cells.at(6)}} | {{b.cells.at(7)}} | {{b.cells.at(8)}}")
  }

  fn can_play(pos: Int) Bool {
    match b.cells.at(pos) {
      s => s.is_empty(),
      _ => false
    }
  }

  fn get_winner() Str? {
    mut res: Str? = option.none()

    if (b.cells.at(0).or("").is_empty() == false) {
      // check top row
      if b.cells.at(0) == b.cells.at(1) and b.cells.at(0) == b.cells.at(2) {
        res = b.cells.at(0)
      // check left column
      } else if b.cells.at(0) == b.cells.at(3) and b.cells.at(0) == b.cells.at(6) {
        res = b.cells.at(0)
      // check top-left to bottom-right
      } else if b.cells.at(0) == b.cells.at(4) and b.cells.at(0) == b.cells.at(8) {
        res = b.cells.at(0)
      }
    }

    // check middle column
    if (b.cells.at(1).or("").is_empty() == false) {
      if b.cells.at(1) == b.cells.at(4) and b.cells.at(1) == b.cells.at(7) {
        res = b.cells.at(1)
      }
    }

    if (b.cells.at(2).or("").is_empty() == false) {
      // check right column
      if b.cells.at(2) == b.cells.at(5) and b.cells.at(2) == b.cells.at(8) {
        res = b.cells.at(2)
      // check top-right to bottom-left
      } else if b.cells.at(2) == b.cells.at(4) and b.cells.at(2) == b.cells.at(6) {
        res = b.cells.at(2)
      }
    }

    // check middle row
    if (b.cells.at(3).or("").is_empty() == false) {
      if b.cells.at(3) == b.cells.at(4) and b.cells.at(3) == b.cells.at(5) {
        res = b.cells.at(3)
      }
    }

    res
  }
}

fn read_move(player: Str) Int {
  io.print("Enter a number between 1 and 9")
  io.print("---------------")

  let input = match Int::from_str(io.read_line()) {
    n => n,
    _ => -1
  }
  match input >= 1 and input <= 9 {
    true => input - 1,
    false => -1
  }
}

mut board = Board{ cells: ["","","","","","","","",""] }

let players = ["X", "O"]

io.print("Starting Game")
io.print("---------------")

board.draw()

while (board.is_full() == false) {
  for player in players {
    io.print("Player {{player}}: Your move")
    mut move = read_move(player)
    while move < 0 {
      io.print("Invalid move")
      move = read_move(player)
    }
    while (board.can_play(move) == false) {
      io.print("Position already played")
      move = read_move(player)
    }

    board.play(player, move)
    io.print("---------------")
    io.print("Player {{player}} played")
    io.print("---------------")
    board.draw()
    io.print("---------------")

    match board.get_winner() {
      winner => {
        io.print("Player {{winner}} wins!")
        break
      },
      _ => {}
    }
  }

  if board.is_full() == true {
    io.print("Game Over! It's a draw")
  }
}
