use ard/io
use ard/maybe

struct Board {
  cells: [Str]
}

impl Board {
  fn mut play(player: Str, pos: Int) {
    @cells.set(pos, player)
  }

  fn is_full() Bool {
    mut full = true
    for cell in @cells {
      if cell.is_empty() {
        full = false
        break
      }
    }
    full
  }

  fn draw() {
    io::print("{@cells.at(0)} | {@cells.at(1)} | {@cells.at(2)}")
    io::print("-------")
    io::print("{@cells.at(3)} | {@cells.at(4)} | {@cells.at(5)}")
    io::print("-------")
    io::print("{@cells.at(6)} | {@cells.at(7)} | {@cells.at(8)}")
  }

  fn can_play(pos: Int) Bool {
    match pos < @cells.size() {
      true => @cells.at(pos).is_empty(),
      false => false
    }
  }

  fn get_winner() Str? {
    mut res: Str? = maybe::none()

    if not @cells.at(0).is_empty() {
      // check top row
      if @cells.at(0) == @cells.at(1) and @cells.at(0) == @cells.at(2) {
        res = maybe::some(@cells.at(0))
      }
      // check left column
      if @cells.at(0) == @cells.at(3) and @cells.at(0) == @cells.at(6) {
        res = maybe::some(@cells.at(0))
      }
      // check top-left to bottom-right
      if @cells.at(0) == @cells.at(4) and @cells.at(0) == @cells.at(8) {
        res = maybe::some(@cells.at(0))
      }
    }

    // check middle column
    if not @cells.at(1).is_empty() {
      if @cells.at(1) == @cells.at(4) and @cells.at(1) == @cells.at(7) {
        res = maybe::some(@cells.at(1))
      }
    }

    if not @cells.at(2).is_empty() {
      // check right column
      if @cells.at(2) == @cells.at(5) and @cells.at(2) == @cells.at(8) {
        res = maybe::some(@cells.at(2))
      }
      // check top-right to bottom-left
      if @cells.at(2) == @cells.at(4) and @cells.at(2) == @cells.at(6) {
        res = maybe::some(@cells.at(2))
      }
    }

    // check middle row
    if not @cells.at(3).is_empty() {
      if @cells.at(3) == @cells.at(4) and @cells.at(3) == @cells.at(5) {
        res = maybe::some(@cells.at(3))
      }
    }

    res
  }
}

fn read_move(player: Str) Int {
  io::print("Enter a number between 1 and 9")
  io::print("---------------")

  let input_str = io::read_line().or("")
  let input = Int::from_str(input_str).or(-1)
  match input >= 1 and input <= 9 {
    true => input - 1,
    false => -1
  }
}

mut board = Board{ cells: ["","","","","","","","",""] }

let players = ["X", "O"]

io::print("Starting Game")
io::print("---------------")

board.draw()

mut finished = false
while not board.is_full() or finished {
  for player in players {
    io::print("Player {player}: Your move")
    mut move = read_move(player)
    while move < 0 {
      io::print("Invalid move")
      move = read_move(player)
    }
    while not board.can_play(move) {
      io::print("Position already played")
      move = read_move(player)
    }

    board.play(player, move)
    io::print("---------------")
    io::print("Player {player} played")
    io::print("---------------")
    board.draw()
    io::print("---------------")

    match board.get_winner() {
      winner => {
        io::print("Player {winner} wins!")
        finished = true
        break
      },
      _ => {}
    }
  }

  if board.is_full() {
    io::print("Game Over! It's a draw")
  }
}
