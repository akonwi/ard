use ard/io
use ard/http
use ard/maybe
use ard/decode

fn main() {
  io::print("GET from pokemon api")
  let res = http::send(http::Request{
    method: http::Method::Get,
    url: "https://pokeapi.co/api/v2/pokemon",
    headers: [:],
  }).expect("Unable to make request")

  if res.is_ok() {
    // Create dynamic data from response body
    let data = decode::any(res.body)
    
    // Extract count field
    let count_result = decode::run(data, decode::field("count", decode::int()))
    
    // Extract results array
    let results_decoder = decode::list(
      decode::map(decode::string(), decode::string())
    )
    let results_result = decode::run(data, decode::field("results", results_decoder))
    
    match count_result {
      ok(count) => io::print("res.count = {count}"),
      err(errors) => io::print("Failed to extract count")
    }
    
    match results_result {
      ok(results) => io::print("res.results.size = {results.size()}"),
      err(errors) => io::print("Failed to extract results")
    }
  } else {
    io::print("Received a {res.status}")
  }

  io::print("http::post() to echoing api")
  let post_res = http::send(http::Request{
    method: http::Method::Post,
    url: "https://postman-echo.com/post",
    headers: ["content-type":"text/plain"],
    body: maybe::some("raw text")
  })
  match post_res {
    ok(res) => io::print("got a {res.status}"),
    err => io::print("unable to make request: {err}")
  }
}
